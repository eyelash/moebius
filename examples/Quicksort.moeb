let std = @import("StandardLibrary.moeb")

let Array = std.Array
let Tuple = std.Tuple
let length = std.length
let get = std.get
let set = std.set
let push = std.push
let putStrLn = std.putStrLn

func swap(a, i, j) = {
	let tmp = a.get(i)
	let a = a.set(i, a.get(j))
	return a.set(j, tmp)
}

let toString = std.toString

func quicksort(a: Array(Int), lo: Int, hi: Int): Array(Int) =
	if (lo >= hi)
		a
	else {
		func partition(a, lo, hi) = {
			let pivot = a.get(hi - 1)
			let i = lo
			let j = lo
			func loop(a, i, j): Tuple((Array(Int), Int)) =
				if (j < hi - 1)
					if (a.get(j) <= pivot) loop(a.swap(i, j), i + 1, j + 1)
					else loop(a, i, j + 1)
				else
					(a, i)
			let (a, i) = loop(a, i, j)
			let a = a.swap(i, hi - 1)
			return (a, i)
		}
		let (a, p) = partition(a, lo, hi)
		let a = quicksort(a, lo, p)
		let a = quicksort(a, p + 1, hi)
		return a
	}

let a = [3, 14, 15, 92, 65, 35, 89, 79, 32, 38, 46]
let hi = a.length()
let a = quicksort(a, 0, hi)
return putStrLn("$a")
