func Array(T) => @arrayType(T)
func Tuple(t) => @tupleType(t)

func putChar(char) => @putChar(char)
func putStr(string) => @putStr(string)
func putStrLn(string) => putStr(string) >> putChar('\n')
func getChar() => @getChar()

func length(array) => @arrayLength(array)
func get(array, index) => @arrayGet(array, index)
func set(array, index, element) => @arraySplice(array, index, 1, element)
func push(array, element) =>
	if (@typeOf(array) == String)
		@stringPush(array, element)
	else
		@arraySplice(array, @arrayLength(array), 0, element)

func toString(x) => {
	func positiveIntToString(int, prefix): String =>
		if (int < 10)
			prefix.push('0' + int)
		else
			positiveIntToString(int / 10, prefix).push('0' + (int % 10))

	func arrayToString(array, index, string): String =>
		if (index < array.length()) {
			let string = if (index > 0) string.push(", ") else string
			let string = string.push(toString(array.get(index)))
			return arrayToString(array, index + 1, string)
		} else
			string.push("]")

	return
		if (@typeOf(x) == String)
			x
		else if (@typeOf(x) == Int)
			if (x < 0)
				positiveIntToString(0 - x, "-")
			else
				positiveIntToString(x, "")
		else
			arrayToString(x, 0, "[")
}

return {
	Array,
	Tuple,
	putChar,
	putStr,
	putStrLn,
	getChar,
	length,
	get,
	set,
	push,
	toString,
}
